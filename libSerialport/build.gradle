plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name('nexus')
                    url uri('https://nexus3.devops.intelab.cloud/repository/android-maven/')
                    credentials {
                        username(localProperties.getProperty("nexusName"))
                        password(localProperties.getProperty("nexusPwd"))
                    }
                }
            }
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = 'com.zhushenwudi.serialport'
                    artifactId = 'serialport'
                    version = '1.5.1'
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
}